{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c989cb2e-0a50-403f-8b58-af61e44a79f3",
   "metadata": {},
   "source": [
    "Some GPU + Windows + current CUDA.jl versions have a known bug with dynamic shared memory allocation.\n",
    "\n",
    "So using static shared memory for running this computation under Windows 10."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "b708af85-d04f-4ab7-9b8e-b294cc626978",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "gpu_scan_static! (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using CUDA\n",
    "\n",
    "function kernel_blelloch_scan_static!(data, n)\n",
    "    T = eltype(data)\n",
    "    sdata = @cuStaticSharedMem(T, 1024)\n",
    "\n",
    "    tid = threadIdx().x\n",
    "    t = tid - 1  # 0-based\n",
    "\n",
    "    if tid <= n\n",
    "        sdata[tid] = data[tid]\n",
    "    else\n",
    "        return\n",
    "    end\n",
    "    sync_threads()\n",
    "\n",
    "    # upsweep\n",
    "    offset = 1\n",
    "    while offset < n\n",
    "        paircount = n ÷ (2 * offset)\n",
    "        if t < paircount\n",
    "            ai = offset*(2t+1)\n",
    "            bi = offset*(2t+2)\n",
    "            sdata[bi] += sdata[ai]\n",
    "        end\n",
    "        offset *= 2\n",
    "        sync_threads()\n",
    "    end\n",
    "\n",
    "    if tid == 1\n",
    "        sdata[n] = zero(T)\n",
    "    end\n",
    "    sync_threads()\n",
    "\n",
    "    # downsweep\n",
    "    offset = n ÷ 2\n",
    "    while offset ≥ 1\n",
    "        paircount = n ÷ (2 * offset)\n",
    "        if t < paircount\n",
    "            ai = offset*(2t+1)\n",
    "            bi = offset*(2t+2)\n",
    "            tmp = sdata[ai]\n",
    "            sdata[ai] = sdata[bi]\n",
    "            sdata[bi] += tmp\n",
    "        end\n",
    "        offset ÷= 2\n",
    "        sync_threads()\n",
    "    end\n",
    "\n",
    "    data[tid] = sdata[tid]\n",
    "    return\n",
    "end\n",
    "\n",
    "function gpu_scan_static!(x::CuArray)\n",
    "    n = length(x)\n",
    "    @assert ispow2(n)\n",
    "    threads = n\n",
    "    @cuda threads=threads kernel_blelloch_scan_static!(x, n)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "09b83d72-fbe0-4636-8476-700de4814ab2",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "10-element Vector{Float32}:\n",
       " 0.0\n",
       " 0.32711983\n",
       " 0.41333407\n",
       " 1.3656528\n",
       " 1.4945819\n",
       " 2.4301267\n",
       " 2.891673\n",
       " 3.301309\n",
       " 4.133217\n",
       " 4.390769"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "N = 1024\n",
    "x = CuArray(rand(Float32, N))\n",
    "gpu_scan_static!(x)\n",
    "collect(x)[1:10]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "24bcc951-d92b-48e2-a111-3af430e9b8e0",
   "metadata": {},
   "source": [
    "CPU serial version for checking and comparison"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "67a27ac5-e5e9-456d-8407-4e517661d680",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "cpu_serial_scan (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using BenchmarkTools\n",
    "\n",
    "# CPU reference exclusive scan (Blelloch-compatible)\n",
    "function cpu_serial_scan(x::Vector{T}) where T\n",
    "    n = length(x)\n",
    "    out = Vector{T}(undef, n)\n",
    "    out[1] = zero(T)\n",
    "    for i in 2:n\n",
    "        out[i] = out[i - 1] + x[i - 1]\n",
    "    end\n",
    "    return out\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2a4f9697-8dfe-47ee-96e8-6c0c32ffd671",
   "metadata": {},
   "source": [
    "Compare"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "f84ad045-bc3f-4e68-9a05-209b5fdcd219",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "First 10 CPU: Float32[0.0, 0.597388, 1.1095417, 1.9304504, 2.0989053, 2.233097, 2.3040352, 2.3629696, 3.33138, 4.0705714]\n",
      "First 10 GPU: Float32[0.0, 0.597388, 1.1095417, 1.9304504, 2.0989056, 2.233097, 2.3040352, 2.3629696, 3.33138, 4.0705714]\n",
      "Correct? true\n",
      "\n",
      "=== Timing ===\n",
      "CPU:\n",
      "  638.922 ns (3 allocations: 4.09 KiB)\n",
      "GPU:\n",
      "  26.700 μs (43 allocations: 720 bytes)\n"
     ]
    }
   ],
   "source": [
    "N = 1024           # must be power of 2, and max is 1024 bcz this is single block version\n",
    "h_input = rand(Float32, N)\n",
    "\n",
    "# CPU serial scan\n",
    "cpu_ref = cpu_serial_scan(h_input)\n",
    "\n",
    "# GPU scan\n",
    "d_input = CuArray(h_input)\n",
    "gpu_scan_static!(d_input)\n",
    "gpu_result = collect(d_input)\n",
    "\n",
    "println(\"First 10 CPU: \", cpu_ref[1:10])\n",
    "println(\"First 10 GPU: \", gpu_result[1:10])\n",
    "println(\"Correct? \", cpu_ref ≈ gpu_result)\n",
    "\n",
    "println(\"\\n=== Timing ===\")\n",
    "\n",
    "println(\"CPU:\")\n",
    "@btime cpu_serial_scan($h_input);\n",
    "println(\"GPU:\")\n",
    "@btime begin\n",
    "    copyto!(d_input, h_input)   # reset input on GPU\n",
    "    gpu_scan_static!(d_input)\n",
    "    synchronize()\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "36dcdaa1-1af3-41a8-9ae8-c4efef272d5d",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.2",
   "language": "julia",
   "name": "julia-1.12.2-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
